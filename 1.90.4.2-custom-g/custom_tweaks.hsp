/*
List of current tweaks with corresponding TweakData entries:

TweakData(x, 0) = "UI Tweaks"
TweakData(0, 0) = "Show Enchant Strength With Objet of Heart."
TweakData(1, 0) = "Allow Spell Tracking."
TweakData(2, 0) = "Add Potentials To Skill Tracker."
TweakData(3, 0) = "Filter Skill Tracker By Potential."
TweakData(4, 0) = "Show Spell Stock In Tracker."
TweakData(50, 0) = "Display Pickpocket shortcut."
TweakData(51, 0) = "Report museum duplicates."

TweakData(x, 1) = "Convenience Tweaks"
TweakData(0, 1) = "Add Feed Option For Pets."
TweakData(1, 1) = "Can Change Item Sprites In Home."
TweakData(2, 1) = "Highlight/Filter Items."
TweakData(3, 1) = "Re-parse ItemList.txt values."
TweakData(50,1) = "Throw potion at self with [t]."

TweakData(x, 2) = "Gameplay Tweaks"
TweakData(0, 2) = "Gather Items At End Of Party Time."
TweakData(1, 2) = "Pets Heal Other Pets."
TweakData(2, 2) = "Share Gold For Group Performance."
TweakData(3, 2) = "Pets die permanently in Loss Mode."
TweakData(4, 2) = "Re-enable save-scumming."
TweakData(5, 2) = "Bash all fruit."
TweakData(6, 2) = "Organize ranch produce."
TweakData(50, 2) = "Everything must go!"
TweakData(51, 2) = "Disable Deep-Sea Castle persistance."
TweakData(52, 2) = "Sandbag training."
TweakData(53, 2) = "No naps while traveling."
TweakData(54, 2) = "Harvest Moon Mode for ranches."

TweakData(x, 3) = "AI Tweaks"
TweakData(0, 3) = "Change Zeome's AI."

TweakData(x, 4) = "Miscellaneous Tweaks"
TweakData(0, 4) = "Enable Random Start For MP3."
TweakData(1, 4) = "Display Weather Everywhere."
TweakData(2, 4) = "Confirm Reload With F2."
TweakData(50, 4) = "Fist of the North Star Mode."
*/

*TweakMenu
	pagesize = 0
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_CATEGORY_UI) = "UI Tweaks"
	listn(0, TWEAK_CATEGORY_CONVENIENCE) = "Convenience Tweaks"
	listn(0, TWEAK_CATEGORY_GAMEPLAY) = "Gameplay Tweaks"
	listn(0, TWEAK_CATEGORY_AI) = "AI Tweaks"
	listn(0, TWEAK_CATEGORY_MISC) = "Miscellaneous Tweaks"
	listn(0, TWEAK_CATEGORY_MAX) = "Exit"
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat TWEAK_CATEGORY_MAX+1
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat TWEAK_CATEGORY_MAX+1
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_CATEGORY_UI ) {
				s = "Toggle tweaks that target UI functions."
			}
			if ( cnt == TWEAK_CATEGORY_CONVENIENCE ) {
				s = "Toggle tweaks that make things more convenient."
			}
			if ( cnt == TWEAK_CATEGORY_GAMEPLAY ) {
				s = "Toggle tweaks that alter parts of the gameplay."
			}
			if ( cnt == TWEAK_CATEGORY_AI ) {
				s = "Toggle tweaks that modify the AI of certain enemies."
			}
			if ( cnt == TWEAK_CATEGORY_MISC ) {
				s = "Toggle tweaks that don't fall into any other category."
			}
			if ( cnt == TWEAK_CATEGORY_MAX ) {
				s = "Exit the menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_CATEGORY_UI) ) {
		goto *UITweakMenu
	}
	if ( key == key_select(TWEAK_CATEGORY_CONVENIENCE) ) {
		goto *ConvenienceTweakMenu
	}
	if ( key == key_select(TWEAK_CATEGORY_GAMEPLAY) ) {
		goto *GameplayTweakMenu
	}
	if ( key == key_select(TWEAK_CATEGORY_AI) ) {
		goto *AITweakMenu
	}
	if ( key == key_select(TWEAK_CATEGORY_MISC) ) {
		goto *MiscTweakMenu
	}
	if ( key == key_select(TWEAK_CATEGORY_MAX) ) {
		gosub *screen_draw
		goto *pc_turn
	}
	if ( key == key_cancel ) {
		gosub *screen_draw
		goto *pc_turn
	}
	goto *TweakMenu
	return

*UITweakMenu
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_UI_SHOW_ENCHANT_STRENGTH) = "Show Enchant Strength With Objet of Heart." + GetTStatus(TWEAK_CATEGORY_UI, TWEAK_UI_SHOW_ENCHANT_STRENGTH)
	listn(0, TWEAK_UI_SPELL_TRACKING) = "Allow Spell Tracking." + GetTStatus(TWEAK_CATEGORY_UI, TWEAK_UI_SPELL_TRACKING)
	listn(0, TWEAK_UI_POTENTIALS_IN_SKILL_TRACKER) = "Add Potentials To Skill Tracker." + GetTStatus(TWEAK_CATEGORY_UI, TWEAK_UI_POTENTIALS_IN_SKILL_TRACKER)
	listn(0, TWEAK_UI_FILTER_SKILL_TRACKER_BY_POTENTIAL) = "Filter Skill Tracker By Potential." + GetTStatus(TWEAK_CATEGORY_UI, TWEAK_UI_FILTER_SKILL_TRACKER_BY_POTENTIAL)
	listn(0, TWEAK_UI_SHOW_SPELLSTOCK_IN_SKILL_TRACKER) = "Show Spell Stock In Tracker." + GetTStatus(TWEAK_CATEGORY_UI, TWEAK_UI_SHOW_SPELLSTOCK_IN_SKILL_TRACKER)
	listn(0, 5) = "Display Pickpocket shortcut." + GetTStatus(0, 50)
	listn(0, 6) = "Report museum duplicates." + GetTStatus(0, 51)
	listn(0, 7) = "Return."
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat 8
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat 8
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_UI_SHOW_ENCHANT_STRENGTH ) {
				s = "When examining items while holding the Objet of Heart, the strength\nof enchantments on the item will be shown."
			}
			if ( cnt == TWEAK_UI_SPELL_TRACKING ) {
				s = "Spells can be added to the skill tracker in the same manner as skills."
			}
			if ( cnt == TWEAK_UI_POTENTIALS_IN_SKILL_TRACKER ) {
				s = "All items in the skill tracker will also display their current potential."
			}
			if ( cnt == TWEAK_UI_FILTER_SKILL_TRACKER_BY_POTENTIAL ) {
				s = "Tracked skills/abilities will not display unless their potential is\nequal to or below a certain threshold."
			}
			if ( cnt == TWEAK_UI_SHOW_SPELLSTOCK_IN_SKILL_TRACKER ) {
				s = "Tracked spells will also display their spell stock."
			}
			if ( cnt == 5 ) {
				s = "Adds a \"Yes/No\" dialogue box when attempting to pick up items that do\nnot belong to you outside of quests and moongates."
			}
			if ( cnt == 6 ) {
				s = "Reports x/y positions of duplicated cards and figurines when the museum updates."
			}
			if ( cnt == 7 ) {
				s = "Return to the previous menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_UI_SHOW_ENCHANT_STRENGTH) ) {
		tc = TWEAK_CATEGORY_UI
		tn = TWEAK_UI_SHOW_ENCHANT_STRENGTH
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_UI_SPELL_TRACKING) ) {
		tc = TWEAK_CATEGORY_UI
		tn = TWEAK_UI_SPELL_TRACKING
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_UI_POTENTIALS_IN_SKILL_TRACKER) ) {
		tc = TWEAK_CATEGORY_UI
		tn = TWEAK_UI_POTENTIALS_IN_SKILL_TRACKER
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_UI_FILTER_SKILL_TRACKER_BY_POTENTIAL) ) {
		tc = TWEAK_CATEGORY_UI
		tn = TWEAK_UI_FILTER_SKILL_TRACKER_BY_POTENTIAL
		ts = 10 // Increments by
		tm = 400 // Max
		gosub *IncrementTweak
	}
	if ( key == key_select(TWEAK_UI_SHOW_SPELLSTOCK_IN_SKILL_TRACKER) ) {
		tc = TWEAK_CATEGORY_UI
		tn = TWEAK_UI_SHOW_SPELLSTOCK_IN_SKILL_TRACKER
		gosub *ToggleTweak
	}
	if ( key == key_select(5) ) {
		tc = 0
		tn = 50
		gosub *ToggleTweak
	}
	if ( key == key_select(6) ) {
		tc = 0
		tn = 51
		gosub *ToggleTweak
	}
	if ( key == key_select(7) | key == key_cancel ) {
		goto *TweakMenu
	}
	goto *UITweakMenu
	return

#defcfunc GetTStatus int TweakCategory, int TweakNumber
	if ( TweakData(TweakNumber, TweakCategory) == 0 ) {
		return " (Currently: Off)"
	}
	if ( TweakCategory == TWEAK_CATEGORY_MISC ) & ( TweakNumber == 50 ) {
		if ( TweakData(TweakNumber, TweakCategory) == 1 ) {
			return " (Currently: Japanese)"
		}
		if ( TweakData(TweakNumber, TweakCategory) == 2 ) {
			return " (Currently: English)"
		}
		else {
			return " (Currently: Romanji)"
		}
	}
	if ( TweakData(TweakNumber, TweakCategory) == 1 ) {
		return " (Currently: On)"
	}
	if ( TweakData(TweakNumber, TweakCategory) > 1 ) {
		return " (Currently: " + TweakData(TweakNumber, TweakCategory) + ")"
	}
	return " FAILED"

*ToggleTweak
	if ( TweakData(tn, tc) == 0 ) {
		TweakData(tn, tc) = 1
	}
	else {
		TweakData(tn, tc) = 0
	}
	return

*IncrementTweak
	TweakData(tn, tc) += ts
	if ( TweakData(tn, tc) > tm ) {
		TweakData(tn, tc) = 0
	}
	return

*ConvenienceTweakMenu
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_CONVENIENCE_FEED_OPTION_PETS) = "Add Feed Option For Pets." + GetTStatus(TWEAK_CATEGORY_CONVENIENCE, TWEAK_CONVENIENCE_FEED_OPTION_PETS)
	listn(0, TWEAK_CONVENIENCE_CHANGE_ITEM_SPRITE_HOME) = "Can Change Item Sprites In Home." + GetTStatus(TWEAK_CATEGORY_CONVENIENCE, TWEAK_CONVENIENCE_CHANGE_ITEM_SPRITE_HOME)
	listn(0, TWEAK_CONVENIENCE_HIGHLIGHT_FILTER) = "Highlight/Filter Items. (Currently: " + TweakData(TWEAK_CONVENIENCE_HIGHLIGHT_FILTER, TWEAK_CATEGORY_CONVENIENCE) + ")"
	listn(0, TWEAK_CONVENIENCE_REPARSE_ITEMLIST) = "Re-parse ItemList.txt."
	listn(0, 4) = "Throw potion at self with [t]." + GetTStatus(1, 50)
	listn(0, 5) = "Return."
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat 6
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat 6
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_CONVENIENCE_FEED_OPTION_PETS ) {
				s = "Adds a Feed option that works like Give but that filters out non-food items when\ninteracting with pets. Also allows for feeding pets raw equipment."
			}
			if ( cnt == TWEAK_CONVENIENCE_CHANGE_ITEM_SPRITE_HOME ) {
				s = "Adds a new menu item to blacksmiths in your home that allows you to change\nitem sprites like at Garok."
			}
			if ( cnt == TWEAK_CONVENIENCE_HIGHLIGHT_FILTER ) {
				s = "Configure your highlighting/filtering choice.\n0 = No Highlight, 1 = Highlight and Mark Items, 2 = Only Show Highlighted Items"
			}
			if ( cnt == TWEAK_CONVENIENCE_REPARSE_ITEMLIST ) {
				s = "Reload the ItemList.txt file, used to enable highlighting and filtering for items."
			}
			if ( cnt == 4 ) {
				s = "Adds potions to the [t]ool menu.\nUsing a potion with this tweak enabled throws it at your tile."
			}
			if ( cnt == 5 ) {
				s = "Return to the previous menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_CONVENIENCE_FEED_OPTION_PETS) ) {
		tc = TWEAK_CATEGORY_CONVENIENCE
		tn = TWEAK_CONVENIENCE_FEED_OPTION_PETS
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_CONVENIENCE_CHANGE_ITEM_SPRITE_HOME) ) {
		tc = TWEAK_CATEGORY_CONVENIENCE
		tn = TWEAK_CONVENIENCE_CHANGE_ITEM_SPRITE_HOME
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_CONVENIENCE_HIGHLIGHT_FILTER) ) {
		tc = TWEAK_CATEGORY_CONVENIENCE
		tn = TWEAK_CONVENIENCE_HIGHLIGHT_FILTER
		ts = 1 // Increments by
		tm = 2 // Max (0, 1, 2)
		gosub *IncrementTweak
	}
	if ( key == key_select(TWEAK_CONVENIENCE_REPARSE_ITEMLIST) ) {
		tc = TWEAK_CATEGORY_CONVENIENCE
		tn = TWEAK_CONVENIENCE_REPARSE_ITEMLIST
		gosub *Load_Item_Highlights
	}
	if ( key == key_select(4) ) {
		tc = 1
		tn = 50
		gosub *ToggleTweak
	}
	if ( key == key_select(5) ) {
		goto *TweakMenu
	}
	if ( key == key_cancel ) {
		goto *TweakMenu
	}
	goto *ConvenienceTweakMenu
	return

*GameplayTweakMenu
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_GAMEPLAY_GATHER_ITEM_PARTYTIME) = "Gather Items At End Of Party Time." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_GATHER_ITEM_PARTYTIME)
	listn(0, TWEAK_GAMEPLAY_PETS_HEAL_PETS) = "Pets Heal Other Pets." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_PETS_HEAL_PETS)
	listn(0, TWEAK_GAMEPLAY_SHARED_PERFORMANCE_GOLD) = "Share Gold For Group Performance." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_SHARED_PERFORMANCE_GOLD)
	listn(0, TWEAK_GAMEPLAY_PETS_DIE_LOSSMODE) = "Pets die permanently in Loss Mode." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_PETS_DIE_LOSSMODE)
	listn(0, TWEAK_GAMEPLAY_ENABLE_SAVE_SCUMMING) = "Re-enable save-scumming." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_ENABLE_SAVE_SCUMMING)
	listn(0, TWEAK_GAMEPLAY_BASH_ALL_FRUIT) = "Bash all fruit." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_BASH_ALL_FRUIT)
	listn(0, TWEAK_GAMEPLAY_ORGANIZE_RANCH) = "Organize ranch produce." + GetTStatus(TWEAK_CATEGORY_GAMEPLAY, TWEAK_GAMEPLAY_ORGANIZE_RANCH)
	listn(0, 7) = "Everything must go!" + GetTStatus(2, 50)
	listn(0, 8) = "Disable Deep-Sea Castle persistance." + GetTStatus(2, 51)
	listn(0, 9) = "Sandbag training." + GetTStatus(2, 52)
	listn(0, 10) = "No naps while traveling." + GetTStatus(2, 53)
	listn(0, 11) = "Harvest Moon Mode for ranches." + GetTStatus(2, 54)
	listn(0, 12) = "Return"

	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat 13
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat 13
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_GAMEPLAY_GATHER_ITEM_PARTYTIME ) {
				s = "When a Party Time! quest ends, you will be presented with a list of items on the\nfloor that you can take."
			}
			if ( cnt == TWEAK_GAMEPLAY_PETS_HEAL_PETS ) {
				s = "Pets with Healing Rain will consider the health of other pets when deciding\nwhether or not to use it. If using custom AI, no effect except\nusage out of combat (requires pet to know it naturally)."
			}
			if ( cnt == TWEAK_GAMEPLAY_SHARED_PERFORMANCE_GOLD ) {
				s = "When performing an ensemble with pets, your pets will receive a portion\nof all gold earned."
			}
			if ( cnt == TWEAK_GAMEPLAY_PETS_DIE_LOSSMODE ) {
				s = "If you are playing in Loss Mode, pets will also have permadeath."
			}
			if ( cnt == TWEAK_GAMEPLAY_ENABLE_SAVE_SCUMMING ) {
				s = "Disables autosaving, re-enables F2 save reloading."
			}
			if ( cnt == TWEAK_GAMEPLAY_BASH_ALL_FRUIT ) {
				s = "Bash all fruit out of fruit trees in one go."
			}
			if ( cnt == TWEAK_GAMEPLAY_ORGANIZE_RANCH ) {
				s = "Better organize the ranch by collecting all ranch produce in one spot (x17 y14)."
			}
			if ( cnt == 7 ) {
				s = "Allows shops to sell furniture, red books, and deeds. Registers,\nshop strongboxes, shelters, and unique items still won't be sold."
			}
			if ( cnt == 8 ) {
				s = "Visiting the Deep-Sea Castle only resets items with a cooldown period\nwhile you're there, instead of continuously resetting them until you sleep."
			}
			if ( cnt == 9 ) {
				s = "Sandbagged monsters will give experience."
			}
			if ( cnt == 10 ) {
				s = "Removes the chance to automatically take a nap when traveling while sleepy."
			}
			if ( cnt == 11 ) {
				s = "Livestock can randomly become sick, are born unable to be milked, etc."
			}
			if ( cnt == 12 ) {
				s = "Return to the previous menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_GAMEPLAY_GATHER_ITEM_PARTYTIME) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_GATHER_ITEM_PARTYTIME
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_PETS_HEAL_PETS) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_PETS_HEAL_PETS
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_SHARED_PERFORMANCE_GOLD) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_SHARED_PERFORMANCE_GOLD
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_PETS_DIE_LOSSMODE) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_PETS_DIE_LOSSMODE
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_ENABLE_SAVE_SCUMMING) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_ENABLE_SAVE_SCUMMING
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_BASH_ALL_FRUIT) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_BASH_ALL_FRUIT
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_GAMEPLAY_ORGANIZE_RANCH) ) {
		tc = TWEAK_CATEGORY_GAMEPLAY
		tn = TWEAK_GAMEPLAY_ORGANIZE_RANCH
		gosub *ToggleTweak
	}
	if ( key == key_select(7) ) {
		tc = 2
		tn = 50
		gosub *ToggleTweak
	}
	if ( key == key_select(8) ) {
		tc = 2
		tn = 51
		gosub *ToggleTweak
	}
	if ( key == key_select(9) ) {
		tc = 2
		tn = 52
		gosub *ToggleTweak
	}
	if ( key == key_select(10) ) {
		tc = 2
		tn = 53
		gosub *ToggleTweak
	}
	if ( key == key_select(11) ) {
		tc = 2
		tn = 54
		gosub *ToggleTweak
	}
	if ( key == key_select(12) | key == key_cancel ) {
		goto *TweakMenu
	}
	goto *GameplayTweakMenu
	return

*AITweakMenu
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_AI_ZEOME) = "Change Zeome's AI." + GetTStatus(TWEAK_CATEGORY_AI, TWEAK_AI_ZEOME)
	listn(0, 1) = "Return."
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat 2
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat 2
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_AI_ZEOME ) {
				s = "Changes Zeome's move list and AI to make the fight significantly more difficult."
			}
			if ( cnt == 1 ) {
				s = "Return to the previous menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_AI_ZEOME) ) {
		tc = TWEAK_CATEGORY_AI
		tn = TWEAK_AI_ZEOME
		gosub *ToggleTweak
	}
	if ( key == key_select(1) ) {
		goto *TweakMenu
	}
	if ( key == key_cancel ) {
		goto *TweakMenu
	}
	goto *AITweakMenu
	return

*MiscTweakMenu
	redraw 0
	s = "Tweak Setup", strhint3b
	display_window (windoww - 680) / 2 + inf_screenx, winposy(448), 640, 448
	s = "Configure Tweaks"
	gosub *screen_drawMsg2

	listn(0, TWEAK_MISC_RANDOM_START_MP3) = "Enable Random Start For MP3." + GetTStatus(4, 0)
	listn(0, TWEAK_MISC_WEATHER_EVERYWHERE) = "Display Weather Everywhere." + GetTStatus(4, 1)
	listn(0, TWEAK_MISC_CONFIRM_RELOAD_F2) = "Confirm Reload With F2." + GetTStatus(4, 2)
	listn(0, TWEAK_MISC_PREVENT_STAYER_MOVEMENT) = "Prevent home stayers from moving." + GetTStatus(TWEAK_CATEGORY_MISC, TWEAK_MISC_PREVENT_STAYER_MOVEMENT)
	listn(0, TWEAK_MISC_PREVENT_BUMP_ATTACK) = "Prevent bump attack." + GetTStatus(TWEAK_CATEGORY_MISC, TWEAK_MISC_PREVENT_BUMP_ATTACK)
	listn(0, 5) = "Fist of the North Star Mode." + GetTStatus(4, 50)
	listn(0, 6) = "Return."
	font lang(cfg_font1, cfg_font2), 14 - en * 2, 0

	repeat 7
		key_list(cnt) = key_select(cnt)
		keyrange = cnt + 1
		pos wx + 38, wy + 40 + cnt * 19 - 2
		gcopy 3, cnt * 24 + 72, 30, 24, 18
		cs_list listn(0, cnt), wx + 64, wy + 40 + cnt * 19 - 1, 19
	loop

	font lang(cfg_font1, cfg_font2), 13 - en * 2, 0

	repeat 7
		if ( listn(0, cs) == listn(0, cnt) ) {
			if ( cnt == TWEAK_MISC_RANDOM_START_MP3 ) {
				s = "Will make MP3s start playback from a random point.\nCould cause compatability issues."
			}
			if ( cnt == TWEAK_MISC_WEATHER_EVERYWHERE ) {
				s = "Weather effects will display in all areas."
			}
			if ( cnt == TWEAK_MISC_CONFIRM_RELOAD_F2 ) {
				s = "If in a mode with save penalty, causes a popup to appear when pressing the\nF2 key to quick-load a save."
			}
			if ( cnt == TWEAK_MISC_PREVENT_STAYER_MOVEMENT ) {
				s = "Prevent your home stayers from moving inside your home."
			}
			if ( cnt == TWEAK_MISC_PREVENT_BUMP_ATTACK ) {
				s = "Prevents you from performing a melee attack when bumping into others."
			}
			if ( cnt == 5 ) {
				s = "Nani?!"
			}
			if ( cnt == 6 ) {
				s = "Return to the previous menu."
			}
			pos wx + 38, wy + 343
			mes s
		}
	loop

	cs_bk = cs
	redraw 1
	await cfg_wait1
	key_check
	cursor_check
	if ( key == key_select(TWEAK_MISC_RANDOM_START_MP3) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = TWEAK_MISC_RANDOM_START_MP3
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_MISC_WEATHER_EVERYWHERE) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = TWEAK_MISC_WEATHER_EVERYWHERE
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_MISC_CONFIRM_RELOAD_F2) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = TWEAK_MISC_CONFIRM_RELOAD_F2
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_MISC_PREVENT_STAYER_MOVEMENT) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = TWEAK_MISC_PREVENT_STAYER_MOVEMENT
		gosub *ToggleTweak
	}
	if ( key == key_select(TWEAK_MISC_PREVENT_BUMP_ATTACK) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = TWEAK_MISC_PREVENT_BUMP_ATTACK
		gosub *ToggleTweak
	}
	if ( key == key_select(5) ) {
		tc = TWEAK_CATEGORY_MISC
		tn = 50
		ts = 1
		tm = 3
		gosub *IncrementTweak
	}
	if ( key == key_select(6) | key == key_cancel ) {
		goto *TweakMenu
	}
	goto *MiscTweakMenu
	return

*ZeomeCustomAI
	if ( InitZeome == 0 ) {
		TCount1 = 5
		InitZeome = 1
	}

	TCount1++

	; If current mana is less than 95% of max, gain 5% max mana.
	if ( (cdata(CDATA_MP, cc) * 100) / cdata(CDATA_MAX_MP, cc) < 95 ) {
		cdata(CDATA_MP, cc) += cdata(CDATA_MAX_MP, cc)/20
	}

	; Set Target to Player
	tc = CHARA_PLAYER

	; Teleport if low on health & MP & player is closer than 6 spaces.
	if ( (cdata(CDATA_HP, cc) * 100) / cdata(CDATA_MAX_HP, cc) < 30 & (cdata(CDATA_MP, cc) * 100) / cdata(CDATA_MAX_MP, cc) < 20 & dist(cdata(CDATA_X, cc), cdata(CDATA_Y, cc), cdata(CDATA_X, tc), cdata(CDATA_Y, tc)) < 8 & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) ) {
		act = SKILL_SPELL_TELEPORT
		efid = act
		npccostmp = 1
		gosub *cast
		if ( stat == 1 ) {
			goto *turn_end
		}
	}

	; Cast Cure of Jua if low on health.
	if ( (cdata(CDATA_HP, cc) * 100)/cdata(CDATA_MAX_HP, cc) < 40 & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) & (cdata(CDATA_MP, cc) * 100)/cdata(CDATA_MAX_MP, cc) > 5 ) {
		act = SKILL_SPELL_CURE_OF_JURE
		efid = act
		npccostmp = 1
		gosub *cast
		if ( stat == 1 ) {
			goto *turn_end
		}
	}

	; If Zeome does not have Holy Shield, Feather, Hero, or Attribute Shield, buff all of them at once. Ignore if silenced.
	if ( (findbuff(cc, BUFF_HOLY_SHIELD) == (-1) | findbuff(cc, BUFF_ATTRIBUTE_SHIELD) == (-1) | findbuff(cc, BUFF_FEATHER) == (-1) | findbuff(cc, BUFF_HERO) == (-1)) & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) ) {
		tcol@txtfunc = 255, 51, 100
		txt "" + name(cc) + " grins wickedly."

		; Feather
		act = SKILL_SPELL_FEATHER
		efid = act
		npccostmp = 1
		gosub *cast

		; Holy shield
		act = SKILL_SPELL_HOLY_SHIELD
		efid = act
		npccostmp = 1
		gosub *cast

		; Attribute Shield
		act = SKILL_SPELL_ATTRIBUTE_SHIELD
		efid = act
		npccostmp = 1
		gosub *cast

		; Hero
		act = SKILL_SPELL_HERO
		efid = act
		npccostmp = 1
		gosub *cast

		; End Turn
		goto *turn_end
	}

	; See if target has Nightmare; if not, cast it. Ignore if Divine Veil is up or if silenced.
	if ( findbuff(tc, BUFF_NIGHTMARE) == (-1) & findbuff(tc, BUFF_HOLY_VEIL) == (-1) & TCount1 > 4 & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) ) {
		act = SKILL_SPELL_NIGHTMARE
		efid = act
		npccostmp = 1
		gosub *cast
		if ( stat == 1 ) {
			TCount1 = 0
			goto *turn_end
		}
	}

	; Shadowstep to player if out of melee & in los
	if ( dist(cdata(CDATA_X, cc), cdata(CDATA_Y, cc), cdata(CDATA_X, tc), cdata(CDATA_Y, tc)) > 1 & fov_los(cdata(CDATA_X, cc), cdata(CDATA_Y, cc), cdata(CDATA_X, tc), cdata(CDATA_Y, tc)) ) {
		act = SKILL_SPACT_SHADOW_STEP
		efid = act
		gosub *action
		if ( stat == 1 ) {
			goto *turn_end
		}
	}

	; 50% chance for Chaos Ball or Melee when in melee range. 12.5% chance to summon.
	if ( dist(cdata(CDATA_X, cc), cdata(CDATA_Y, cc), cdata(CDATA_X, tc), cdata(CDATA_Y, tc)) == 1 ) {
		if ( rnd(6) == 0 & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) ) {
			act = SKILL_SPELL_SUMMON_MONSTERS
			efid = act
			npccostmp = 1
			gosub *cast
			if ( stat == 1 ) {
				goto *turn_end
			}
		}

		if ( rnd(3) == 0 & findbuff(cc, BUFF_MIST_OF_SILENCE) == (-1) ) {
			act = SKILL_SPELL_CHAOS_BALL
			efid = act
			npccostmp = 1
			gosub *cast
			if ( stat == 1 ) {
				goto *turn_end
			}
		}
		else {
			gosub *act_melee
			goto *turn_end
		}
	}
	goto *ai_followMove
	return

/********** ANNA CUSTOM - ENDING **********/

